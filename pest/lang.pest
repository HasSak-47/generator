definitions = {
    SOI ~ (model_definition | ty_definition | end_point)+ ~ EOI
}

ty_definition = { "type" ~ name ~ "=" ~ ty }

model_definition = {
    "type" ~ name ~ model_extension? ~ "=" ~ model_type
}

model_extension = @{ ":" ~ name }

end_point = {
    name ~ "(" ~ (member_field ~ ("," ~ member_field)* ~ ","?)? ~ ")" ~ operation_mark ~ path ~ ("->" ~ ty)?
}

operation_mark = ${ "@" ~ operation }
operation      =  { "get" | "post" | "put" }

path = ${ "\"" ~ inner ~ "\"" }

ty = ${ (model_type | sum_type | map_type | value_type | into_type | repr_type | primitive_type | null_type | named_type) ~ weird_mark* }

model_type = {
    "{" ~ member_field ~ ("," ~ member_field)* ~ ","? ~ "}"
}

member_field = ${ name ~ " "* ~ ":" ~ " "* ~ ty }
into_type    = ${ primitive_type ~ " "* ~ "as" ~ " "* ~ repr_type }

map_type   = ${ "<" ~ " "* ~ primitive_type ~ " "* ~ "," ~ " "* ~ ty ~ " "* ~ ">" }
named_type = ${ name }

value_type  = ${ value }
sum_type    = ${ sum_tag ~ "{" ~ ty ~ " "* ~ "|" ~ " "* ~ ty ~ "}" }
sum_tag     =  { "inner" | "outer" }
weird_mark  = ${ (option_mark | array_mark) }
option_mark = @{ "?" }
array_mark  =  { "[" ~ prec? ~ "]" }

repr_type = ${ repr_root ~ ("_" ~ prec)? }
repr_root = ${ datetime }

datetime     = ${ "datetime" ~ ("(" ~ datetime_fmt ~ ")")? }
datetime_fmt =  { "iso" | name }

primitive_type = ${ praw | pprec ~ ("_" ~ prec)? }
praw           =  { "bool" }
pprec          =  { "int" | "uint" | "float" | "string" }

null_type = { "null" }

value  = ${ string | uint | int | float | bool }
bool   = @{ "true" | "false" }
float  = @{ sign? ~ (ASCII_DIGIT)+ ~ ("." ~ ASCII_DIGIT*)? }
int    = @{ sign? ~ (ASCII_DIGIT)+ }
uint   = @{ (ASCII_DIGIT)+ }
sign   = @{ "-" | "+" }
name   = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")+ }
prec   = @{ ASCII_DIGIT+ }
string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

WHITESPACE = _{ " " | "\t" | "\n" }
COMMENT    = _{ "#" ~ (!"\n" ~ ANY)* }
